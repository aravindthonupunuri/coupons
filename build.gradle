buildscript {
    repositories {
        jcenter()
        maven { url "https://binrepo.target.com/artifactory/platform" }
    }
    dependencies {
        classpath "com.target.platform:platform-connector-gradle:${tapPlatformConnectorVersion}"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.72" apply false
    id "org.jetbrains.kotlin.kapt" version "1.3.72" apply false
    id "org.jetbrains.kotlin.plugin.allopen" version "1.3.72" apply false
    id "org.jlleitschuh.gradle.ktlint" version "8.2.0" apply false
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC15" apply false
    id "org.sonarqube" version "2.7.1" apply false
    id "org.flywaydb.flyway" version "6.0.4" apply false
    id "maven-publish"
}

apply plugin: "com.target.platform.connector"
project.tasks.startScripts.enabled = false

allprojects {
    apply plugin: "java"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"
    apply plugin: "org.jetbrains.kotlin.plugin.allopen"
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply plugin: "org.sonarqube"
    apply plugin: "groovy"
    apply plugin: "jacoco"
    apply plugin: "org.flywaydb.flyway"

    repositories {
        mavenCentral()
        maven { url "https://jcenter.bintray.com" }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url "https://packages.confluent.io/maven/" }
        maven { url "https://binrepo.target.com/artifactory/TargetOSS" }
        maven { url "https://binrepo.target.com/artifactory/lists-v4" }
        maven { url "https://binrepo.target.com/artifactory/platform" }
        mavenLocal()
    }

    sourceCompatibility = 11
    targetCompatibility = 11

    dependencies {
        implementation platform("io.micronaut:micronaut-bom:$micronautVersion")
        implementation "io.micronaut.reactor:micronaut-reactor"
        implementation "io.projectreactor.kotlin:reactor-kotlin-extensions:1.1.0-M1"
        implementation "io.micronaut.reactor:micronaut-reactor-http-client:1.0.0"

        implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.72"
        implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.72"
        implementation "io.github.microutils:kotlin-logging:1.6.24"

        implementation "io.micronaut:micronaut-runtime"
        implementation "io.micronaut:micronaut-http-client"

        implementation "io.micronaut:micronaut-http-server-netty"
        implementation "io.micronaut:micronaut-validation"
        implementation "io.micronaut.beanvalidation:micronaut-hibernate-validator"
        implementation "io.micronaut.cache:micronaut-cache-core"
        implementation "javax.annotation:javax.annotation-api"
        implementation "com.lmax:disruptor:3.4.2"

        implementation("com.tgt.lists:atlas-client:2.231.1")
        implementation("com.tgt.lists:lists-components:2.231.6") {
            exclude group: 'com.tgt.lists.micronaut', module: 'micronaut-resilience4j'
        }

        implementation("com.tgt.backpackregistry:backpack-transactions-client:2.231.3") {
            exclude group: 'com.tgt.backpackregistry', module: 'backpack-registry-client'
            exclude group: 'com.tgt.lists', module: 'atlas-client'
        }
        implementation("com.tgt.backpackregistry:backpack-registry-client:2.231.17") {
            exclude group: 'com.tgt.lists', module: 'lists-components'
            exclude group: 'com.tgt.lists', module: 'atlas-client'
        }

        implementation "com.tgt.cronbeacon:beacon-client:2.231.0"
        implementation "com.tgt.lists:lists-permissions-client:2.231.0"

        implementation "com.tgt.lists:lists-oauth:2.231.0"
        implementation "com.tgt.lists.micronaut:micronaut-zipkin:2.231.0"
        implementation "com.tgt.lists:lists-message-bus:2.231.2"
        implementation "com.tgt.lists:notification-tracer-client:1.0.1"
        implementation "com.tgt.lists:lists-logging:2.231.0"
        implementation 'com.tgt.lists.micronaut:micronaut-jdbc:2.201.0'
        implementation "com.target.oss:platform-connector-micronaut:${micronautTapPlatformConnectorVersion}"
        implementation "io.swagger.core.v3:swagger-annotations"
        implementation "com.tgt.lists.micronaut:micronaut-metrics:2.231.0"
        implementation "io.micronaut.kotlin:micronaut-kotlin-runtime"
        implementation "io.micronaut:micronaut-runtime"

        implementation "io.micronaut.data:micronaut-data-processor"
        implementation "io.micronaut.data:micronaut-data-jdbc"

        compileOnly "jakarta.persistence:jakarta.persistence-api:2.2.2"
        runtimeOnly "org.postgresql:postgresql:42.2.4"
        runtimeOnly "io.micronaut.sql:micronaut-jdbc-hikari:3.0.0"

        kapt "io.micronaut.configuration:micronaut-openapi:1.5.0"
        kapt platform("io.micronaut:micronaut-bom:$micronautVersion")
        kapt "io.micronaut:micronaut-inject-java"
        kapt "io.micronaut:micronaut-validation"
        kaptTest platform("io.micronaut:micronaut-bom:$micronautVersion")
        kaptTest "io.micronaut:micronaut-inject-java"

        runtime "com.fasterxml.jackson.core:jackson-databind:2.11.2"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.2"

        testImplementation platform("io.micronaut:micronaut-bom:$micronautVersion")
        testImplementation "io.micronaut:micronaut-inject-groovy:$micronautVersion"
        testImplementation("org.spockframework:spock-core") {
            exclude group: "org.codehaus.groovy", module: "groovy-all"
        }
        testImplementation "org.codehaus.groovy:groovy-json:3.0.5"
        testImplementation "io.micronaut.test:micronaut-test-spock"
        testImplementation "com.tgt.swaggerlib:swagger-sync:1.210.16"
        testImplementation "com.tgt.lists.micronaut:micronaut-test:2.231.0"
        testImplementation("org.objenesis:objenesis:2.6")
        testImplementation 'io.micronaut.micrometer:micronaut-micrometer-registry-prometheus'
        testImplementation "io.micronaut.sql:micronaut-jdbc-hikari:3.0.0"
        testImplementation "io.micronaut.flyway:micronaut-flyway"
        testImplementation "org.testcontainers:spock:1.12.2"
        testImplementation "org.testcontainers:postgresql:1.12.2"
        testImplementation "org.postgresql:postgresql:42.2.4"
        testImplementation "cglib:cglib:3.3.0"
        testImplementation group: 'io.opentracing', name: 'opentracing-mock', version: '0.33.0'
        testImplementation "org.testcontainers:kafka:1.14.3"
        testImplementation "com.tgt.lists:grafana-builder:2.231.18"
    }

    configurations.all {
        exclude group: "ch.qos.logback"
        exclude group: 'com.tgt.lists', 'module': 'platform-connector-micronaut'
    }

    jar {
        exclude('**/log4j*.xml')
        exclude('**/logback*.xml')
        exclude('**/application*.yml')
        exclude('**/proxy*.yml')
        from('api-specs') {
            into 'apispec'
        }
    }

    allOpen {
        annotation("io.micronaut.http.annotation.Controller")
        annotation("javax.inject.Singleton")
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
            //Will retain parameter names for Java reflection
            javaParameters = true
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "11"
            javaParameters = true
        }
    }

    flyway {
        // ./gradlew -Ppostgres_host=localhost -Ppostgres_ssl='' -Ppostgres_user=postgres -Ppostgres_pwd=postgres flywayMigrate -i
        url = "jdbc:postgresql://${postgres_host}:5432/postgres${postgres_ssl}"
        user = "${postgres_user}"
        password = "${postgres_pwd}"
        schemas = ['registrycoupons']
    }

    detekt {
        parallel = true
        config = files("src/main/resources/detekt.yml")
        input = files(
            "src/main/kotlin/com/tgt/lists/api"
        )

        reports {
            html {
                enabled = true
                destination = file("build/reports/detekt.html")
            }
        }

        idea {
            path = ".idea"
            codeStyleScheme = ".idea/idea-code-style.xml"
            inspectionsProfile = ".idea/inspect.xml"
            report = "$project.projectDir/reports"
            mask = "*.kt,"
        }
    }
}

task downloadDependencies() {
    doLast {
        allprojects { p ->
            configurations.each { c ->
                if (c.canBeResolved) {
                    println "Downloading dependencies for $p.path - $c.name"
                    c.files
                }
            }
        }
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username = System.getProperty('USERNAME')
                password = System.getProperty('PASSWORD')
            }
            url 'https://binrepo.target.com/artifactory/lists-v4'
        }
    }
}
