plugins {
    id "application"
}

apply from: "test.gradle"
apply plugin: "com.target.platform.connector"

mainClassName = "com.tgt.backpackregistrycoupons.api.Application"

distTar {
    archiveFileName = project.name + ".tar"
}

dependencies {
    implementation project(":backpack-registry-coupons-service")
    implementation "io.micronaut.kafka:micronaut-kafka"
    implementation "org.owasp.esapi:esapi:2.2.0.0"
    implementation "com.tgt.lists.micronaut:micronaut-resilience4j:$micronaut_resilience4j_version"
    testImplementation project(':backpack-registry-coupons-service').sourceSets.test.output
}

def clientJarName = 'backpack-coupons-client'
task clientJar(type: Jar, dependsOn: classes) {
    baseName = clientJarName
    version =  System.properties["VERSION"]
    includeEmptyDirs = false
    from (sourceSets.main.output)
    from (project(':backpack-registry-coupons-service').sourceSets.main.output) {
        // exclude backpack-registry-coupons-service module's META-INF/services
        exclude "**\\META-INF\\services\\**\\*"
    }
    // META-INF/services should be included when any of the included classes use micronaut annotations e.g. @Client, @Filter etc.
    // Include Enum classes with <Classname>* rather than <Classname>.* as it may cause issues with Enums using companion objects
    // Include micronaut's $ prefixed and suffixed classes for compiled time injection to work
    include ("**\\META-INF\\services\\**\\*", "**\\transport\\**\\*", "**\\util\\**")
    exclude ("**\\util\\Helper*")
    from (project(':backpack-registry-coupons-app').sourceSets.main.output)
    include ("**\\client\\**\\*")
}
task clientSourcesJar(type: Jar, dependsOn: classes) {
    baseName = clientJarName
    version =  System.properties["VERSION"]
    classifier = 'sources'
    includeEmptyDirs = false
    from (project(':backpack-registry-coupons-service').sourceSets.main.kotlin, sourceSets.main.kotlin)
    include ("**\\transport\\**\\*", "**\\util\\**")
    from (project(':backpack-registry-coupons-app').sourceSets.main.output)
    include ("**\\client\\**\\*")
}

publishing {
    publications {
        client(MavenPublication) {
            artifactId = 'backpack-coupons-client'
            version = System.properties["VERSION"]
            artifact tasks.clientJar
            artifact tasks.clientSourcesJar
        }
    }
}
